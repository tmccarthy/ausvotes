plugins {
    id 'com.github.maiflai.scalatest' version '0.19'
    id 'scala'
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.228'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        // Needed for scalaz-zio
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile "org.scala-lang:scala-library:$scalaVersion"

    compile project(':core')
    compile project(':shared:aws')
    compile project(':shared:recountResources')

    compile("org.scalaz:scalaz-zio${s}:$scalazZioVersion") { changing = true }

    compile 'com.amazonaws:aws-java-sdk-s3'

    testCompile "org.scalatest:scalatest${s}:$scalaTestVersion"
    testCompile "au.id.tmm:tmm-test-utils${s}:$tmmTestUtilsVersion"

    // Needed to produce scalatest report
    testRuntime 'org.pegdown:pegdown:1.4.2'
}

task uploadRecountData(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'au.id.tmm.ausvotes.tasks.generatepreferencetrees.Main'

    args rootProject.file('rawData').absolutePath,
            project(':infrastructure').recountDataBucketName,
            '2016'

    jvmArgs '-Xmx3G'
}
