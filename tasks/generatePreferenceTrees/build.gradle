import au.id.tmm.ausvotes.buildsrc.DeleteS3Task

plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

apply plugin: au.id.tmm.ausvotes.buildsrc.MyScalaPlugin

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.228'
    }
}

dependencies {
    compile project(':core')
    compile project(':shared:aws')
    compile project(':shared:io')
    compile project(':shared:recountResources')

    compile "org.scalaz:scalaz-zio${s}:$scalazZioVersion"

    compile 'com.amazonaws:aws-java-sdk-s3'
}

task deleteRecountData(type: DeleteS3Task) {
    bucket = project(':infrastructure').recountDataBucketName
    prefix = 'recountData/2016/'
}

task uploadRecountData(type: JavaExec) {
    dependsOn tasks.deleteRecountData

    classpath = sourceSets.main.runtimeClasspath

    main = 'au.id.tmm.ausvotes.tasks.generatepreferencetrees.Main'

    args rootProject.file('rawData').absolutePath,
            project(':infrastructure').recountDataBucketName,
            '2016'

    jvmArgs '-Xmx3G'
}
