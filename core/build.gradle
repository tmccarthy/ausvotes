plugins {
    id 'com.github.maiflai.scalatest' version '0.19'
    id 'scala'
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    testOutput
}

sourceSets {
    integrationTest {
        scala {
            srcDirs = ['src/integration-test/scala']
        }
        resources {
            srcDirs = ['src/integration-test/resources']
        }
    }
}

dependencies {
    compile "org.scala-lang:scala-library:$scalaVersion"
    compile "com.typesafe.scala-logging:scala-logging${s}:3.7.2"

    compile "au.id.tmm:tmm-utils${s}:$tmmUtilsVersion"
    compile "au.id.tmm:countstv${s}:0.1.6"

    compile "com.github.scopt:scopt${s}:3.6.0"
    compile "com.github.tototoshi:scala-csv${s}:1.3.4"

    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.4'

    compile 'org.slf4j:slf4j-simple:1.7.25'

    testCompile "org.scalatest:scalatest${s}:$scalaTestVersion"
    testCompile "org.scalamock:scalamock-scalatest-support${s}:$scalaMockVersion"
    testCompile "au.id.tmm:tmm-test-utils${s}:$tmmTestUtilsVersion"

    // Needed to produce scalatest report
    testRuntime 'org.pegdown:pegdown:1.4.2'

    integrationTestCompile sourceSets.main.output
    integrationTestCompile sourceSets.test.output
    integrationTestCompile configurations.testCompile
    integrationTestRuntime configurations.testRuntime

    testOutput sourceSets.test.output
}

task integrationTest(type: Test) {
    shouldRunAfter tasks.test
    testClassesDirs = sourceSets.integrationTest.output
    classpath = files(sourceSets.integrationTest.runtimeClasspath, sourceSets.test.runtimeClasspath)
}

check.dependsOn integrationTest
