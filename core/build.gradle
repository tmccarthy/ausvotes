apply plugin: au.id.tmm.ausvotes.buildsrc.MyScalaPlugin

configurations {
    testOutput
}

sourceSets {
    integrationTest {
        scala {
            srcDirs = ['src/integration-test/scala']
        }
        resources {
            srcDirs = ['src/integration-test/resources']
        }
    }
}

dependencies {
    compile project(':core:model')
    compile project(':core:dataSources')
    compile project(':shared:io')

    compile "org.scalaz:scalaz-zio-interop-future${s}:$scalazZioVersion"

    compile "io.circe:circe-parser${s}:$circeVersion"

    compile "com.github.tototoshi:scala-csv${s}:1.3.4"

    compile "commons-io:commons-io:$commonsIoVersion"
    compile "org.apache.commons:commons-lang3:$commonsLangVersion"

    compile 'org.slf4j:slf4j-simple:1.7.25'
    compile "com.typesafe.scala-logging:scala-logging${s}:3.7.2"

    testCompile project(':core:fixtures')
    testCompile "org.scalamock:scalamock-scalatest-support${s}:$scalaMockVersion"

    integrationTestCompile sourceSets.main.output
    integrationTestCompile sourceSets.test.output
    integrationTestCompile configurations.testCompile
    integrationTestRuntime configurations.testRuntime

    testOutput sourceSets.test.output
}

task integrationTest(type: Test) {
    shouldRunAfter tasks.test
    testClassesDirs = sourceSets.integrationTest.output
    classpath = files(sourceSets.integrationTest.runtimeClasspath, sourceSets.test.runtimeClasspath)
}

check.dependsOn integrationTest
