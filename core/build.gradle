plugins {
    id "com.github.maiflai.scalatest" version "0.16"
    id "org.scoverage" version "2.1.0"
}

apply plugin: 'scala'

repositories {
    mavenCentral()
    maven {
        url 'http://artifactory.ambitious.tools/artifactory/sbt-libs-release-local/'
    }
}

sourceSets {
    integrationTest {
        scala {
            srcDirs = ['src/integration-test/scala']
        }
        resources {
            srcDirs = ['src/integration-test/resources']
        }
    }
    testScoverage {
        compileClasspath.add(configurations.integrationTestCompile)
        runtimeClasspath.add(configurations.integrationTestRuntime)
    }
}

dependencies {
    compile "org.scala-lang:scala-library:$scalaVersion"

    compile "au.id.tmm:tmmutils${s}:$tmmUtilsVersion"

    compile "com.github.scopt:scopt${s}:3.6.0"
    compile "com.github.tototoshi:scala-csv${s}:1.3.4"

    compile "com.typesafe.akka:akka-actor${s}:$akkaVersion"

    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.4'

    testCompile "org.scalatest:scalatest${s}:$scalaTestVersion"
    testCompile "org.scalamock:scalamock-scalatest-support${s}:$scalaMockVersion"
    testCompile "au.id.tmm:tmmtestutils${s}:$tmmUtilsVersion"

    // Needed to produce scalatest report
    testRuntime 'org.pegdown:pegdown:1.4.2'

    scoverage "org.scoverage:scalac-scoverage-plugin${s}:$scoverageVersion"
    scoverage "org.scoverage:scalac-scoverage-runtime${s}:$scoverageVersion"

    integrationTestCompile sourceSets.main.output
    integrationTestCompile sourceSets.test.output
    integrationTestCompile configurations.testCompile
    integrationTestRuntime configurations.testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output
    classpath = files(sourceSets.integrationTest.runtimeClasspath, sourceSets.test.runtimeClasspath)
}

check.dependsOn integrationTest

// TODO code coverage tasks
