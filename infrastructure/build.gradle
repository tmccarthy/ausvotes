import au.id.tmm.ausvotes.buildsrc.DeleteS3Task
import au.id.tmm.ausvotes.buildsrc.EcrConfig
import seek.aws.s3.UploadFiles

plugins {
    id "seek.aws" version "0.0.26"
    id "seek.cloudformation" version "0.0.26"
}

[
    ':lambdas:recount',
    ':lambdas:recountEnqueue',
].each {
    evaluationDependsOn(it)
}

ext.awsRegion = 'ap-southeast-2'
ext.recountDataBucketName = 'recount-data.buckets.ausvotes.info'
ext.ecrRepoId = 'au.id.tmm.ecr-repository'
ext.awsAccountId = '327455522484'

EcrConfig.addEcrTasks(project, awsAccountId, awsRegion, [
    project(':ui').getTasksByName('dockerBuild', false).first()
])

aws {
    region awsRegion
}

cloudFormation {
    stackName rootProject.name
    templateFile file('src/app-stack.cf.yaml')
    parameters ([
        DeploymentArtefactBucket: deploymentBucket,
        RecountLambdaCodeKey: project(':lambdas:recount').ext.deploymentBucketKey,
        RecountEnqueueLambdaCodeKey: project(':lambdas:recountEnqueue').ext.deploymentBucketKey,
        RecountDataBucketName: recountDataBucketName,
        AusvotesUiDockerImage: tasks.pushUiImageToEcr.imageName
    ])
}

task uploadTemplatesToS3(type: UploadFiles) {
    files fileTree('src/templates').include('**/*.yaml')
    bucket deploymentBucket
    prefix 'au.id.tmm/ausvotes/cf/templates'
    cleanPrefixBeforeUpload true
    interpolate false
}

[
    project(':lambdas:recount').tasks.uploadToDeploymentBucket,
    project(':lambdas:recountEnqueue').tasks.uploadToDeploymentBucket,
    tasks.uploadTemplatesToS3
].each {
    tasks.createOrUpdateStack.dependsOn(it)
}

task deployIfMaster(type: GradleBuild) {
    onlyIf {
        grgit.open(dir: rootProject.projectDir).branch.current.name == 'master' ||
            System.env.TRAVIS_BRANCH == 'master'
    }

    startParameter.logLevel = LogLevel.INFO
    startParameter.showStacktrace = ShowStacktrace.ALWAYS_FULL

    tasks = [':infrastructure:pushAllImagesToEcr', ':infrastructure:createOrUpdateStack']
}

task clearRecountsCache(type: DeleteS3Task, group: 'build') {
    bucket = recountDataBucketName
    prefix = 'recounts/'
}
