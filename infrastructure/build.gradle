plugins {
    id "seek.aws" version "0.0.26"
    id "seek.cloudformation" version "0.0.26"
}

evaluationDependsOn(':lambdas:recount')

aws {
    region 'ap-southeast-2'
}

ext.recountDataBucketName = 'recount-data.buckets.ausvotes.info'

cloudFormation {
    stackName rootProject.name
    templateFile file('src/app-stack.cf.yaml')
    parameters ([
            DeploymentArtefactBucket: deploymentBucket,
            RecountLambdaCodeKey: project(':lambdas:recount').ext.deploymentBucketKey,
            RecountDataBucketName: recountDataBucketName,
    ])
}

tasks.createOrUpdateStack.dependsOn(project(':lambdas:recount').tasks.uploadToDeploymentBucket)

task deployIfMaster(type: GradleBuild) {
    onlyIf { grgit.open(dir: rootProject.projectDir).branch.current.name == 'master' }

    startParameter.logLevel = LogLevel.INFO
    startParameter.showStacktrace = ShowStacktrace.ALWAYS_FULL

    tasks = [':infrastructure:createOrUpdateStack']
}
