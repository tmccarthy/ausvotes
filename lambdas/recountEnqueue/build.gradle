import seek.aws.s3.UploadFile

plugins {
    id 'com.github.maiflai.scalatest' version '0.19'
    id 'scala'
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "seek.aws" version "0.0.26"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        // Needed for scalaz-zio
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.228'
    }
}

dependencies {
    compile "org.scala-lang:scala-library:$scalaVersion"

    compile project(':lambdas:lambdaUtilities')
    compile project(':shared:recountResources')

    compile 'com.amazonaws:aws-java-sdk-s3'
    compile 'com.amazonaws:aws-java-sdk-sns'
    compile 'com.amazonaws:aws-lambda-java-core:1.2.0'

    compile "org.scalaz:scalaz-zio${s}:$scalazZioVersion"

    testCompile project(':core:fixtures')
    testCompile "org.scalatest:scalatest${s}:$scalaTestVersion"
    testCompile "au.id.tmm:tmm-test-utils${s}:$tmmTestUtilsVersion"

    // Needed to produce scalatest report
    testRuntime 'org.pegdown:pegdown:1.4.2'
}

task buildZip(type: Zip) {
    from compileJava
    from compileScala
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

assemble.dependsOn buildZip

ext.deploymentBucketKey = "$rootProject.group/$rootProject.name/$buildZip.archiveName"

task uploadToDeploymentBucket(type: UploadFile) {
    dependsOn buildZip
    file buildZip.archivePath
    bucket deploymentBucket
    key deploymentBucketKey
    failIfObjectExists false
}
