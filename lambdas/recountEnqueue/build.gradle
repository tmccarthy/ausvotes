import seek.aws.s3.UploadFile

plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "seek.aws" version "0.0.26"
}

apply plugin: au.id.tmm.ausvotes.buildsrc.MyScalaPlugin

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.228'
    }
}

dependencies {
    compile project(':lambdas:lambdaUtilities')
    compile project(':shared:recountResources')

    compile 'com.amazonaws:aws-java-sdk-s3'
    compile 'com.amazonaws:aws-java-sdk-sns'
    compile 'com.amazonaws:aws-lambda-java-core:1.2.0'

    compile "org.scalaz:scalaz-zio${s}:$scalazZioVersion"

    testCompile project(':core:fixtures')
}

task buildZip(type: Zip) {
    from compileJava
    from compileScala
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

assemble.dependsOn buildZip

ext.deploymentBucketKey = "$rootProject.group/$rootProject.name/$buildZip.archiveName"

task uploadToDeploymentBucket(type: UploadFile) {
    dependsOn buildZip
    file buildZip.archivePath
    bucket deploymentBucket
    key deploymentBucketKey
    failIfObjectExists false
}
