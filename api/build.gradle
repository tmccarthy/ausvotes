plugins {
    id 'com.github.maiflai.scalatest' version '0.16'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'scala'
}

apply plugin: 'war'
apply plugin: au.id.tmm.ausvotes.buildsrc.MyVersionPlugin

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    testOutput
}

sourceSets {
    integrationTest {
        scala {
            srcDirs = ['src/integration-test/scala']
        }
        resources {
            srcDirs = ['src/integration-test/resources']
        }
    }
}

def scalatraVersion = "2.5.1"

dependencies {
    compile project(':backend')

    compile "org.scalatra:scalatra${s}:$scalatraVersion"
    compile "org.scalatra:scalatra-json${s}:$scalatraVersion"
    compile "org.json4s:json4s-jackson${s}:3.5.3"
    compile "ch.qos.logback:logback-classic:1.1.7"
    compile 'org.eclipse.jetty:jetty-webapp:9.4.7.v20170914'

    testCompile "org.scalatest:scalatest${s}:2.2.1"
    testCompile "org.scalamock:scalamock-scalatest-support${s}:3.6.0"
    testCompile "au.id.tmm:tmm-test-utils${s}:$tmmUtilsVersion"
    testCompile 'org.slf4j:slf4j-simple:1.7.25'

    // Needed to produce scalatest report
    testRuntime 'org.pegdown:pegdown:1.4.2'

    integrationTestCompile sourceSets.main.output
    integrationTestCompile sourceSets.test.output
    integrationTestCompile configurations.testCompile
    integrationTestRuntime configurations.testRuntime
}

task integrationTest(type: Test) {
    shouldRunAfter tasks.test
    testClassesDirs = sourceSets.integrationTest.output
    classpath = files(sourceSets.integrationTest.runtimeClasspath, sourceSets.test.runtimeClasspath)
}

check.dependsOn integrationTest

jar {
    manifest {
        attributes 'Main-Class': 'au.id.tmm.ausvotes.api.JettyLauncher'
    }
}

shadowJar {
    baseName = 'ausvotes-api'
}

tasks.build.dependsOn(tasks.shadowJar)
